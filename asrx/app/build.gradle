plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystorePropertiesDir = rootProject.file("${rootDir}/config")
def keystorePropertiesFile = rootProject.file("${keystorePropertiesDir}/keystore.properties")
if (!keystorePropertiesDir.isDirectory()) keystorePropertiesDir.mkdirs()
if (!keystorePropertiesFile.isFile()) keystorePropertiesFile.createNewFile()
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace applicationId
    compileSdk 33

    defaultConfig {
        applicationId applicationId
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField("String[]", "PRODUCT_IDS", productIds)

        // RuStore
        buildConfigField("String", "RUSTORE_APP_ID", "\"$ruStoreAppId\"")
        // Deeplink scheme
        buildConfigField("String", "DEEPLINK_RU_STORE", "\"$ruStoreDeeplink\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        releaseRuStore {
            storeFile file("${rootDir}/config/keystore")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        releaseRuStore {
            initWith release
            matchingFallbacks = ['release']
            signingConfig signingConfigs.releaseRuStore
        }
        debugRuStore {
            initWith debug
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.releaseRuStore
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // RxJava https://github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 3.x.x version)
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'

    // https://developer.android.com/google/play/billing/integrate
    api 'com.android.billingclient:billing:4.0.0'
    // https://help.rustore.ru/rustore/for_developers/developer-documentation/sdk_payments/SDK-connecting-payments/quick_start
    api 'ru.rustore.sdk:billingclient:0.1.6'
}